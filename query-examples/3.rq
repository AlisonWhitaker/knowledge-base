PREFIX mus: <http://data.doremus.org/ontology#>
PREFIX ecrm: <http://erlangen-crm.org/current/>
PREFIX efrbroo: <http://erlangen-crm.org/efrbroo/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

# [en] Which works have been written for string quartet?
# [fr] Quelles oeuvres ont été écrites pour un quatuor à cordes ?


SELECT DISTINCT ?expression, SAMPLE(?title) as ?title, ?casting
WHERE {
  ?expression a efrbroo:F22_Self-Contained_Expression ;
          mus:U70_has_title ?title ;
          mus:U13_has_casting ?casting .
  
  {
    SELECT DISTINCT ?casting
    WHERE {
        ?casting mus:U23_has_casting_detail ?allCastingDets .
        ?casting mus:U23_has_casting_detail ?castingDet1 .
        ?castingDet1 mus:U2_foresees_use_of_medium_of_performance_of_type <http://data.doremus.org/vocabulary/iaml/mop/svl> ;
                mus:U30_foresees_quantity_of_mop 2 .

        
        ?casting mus:U23_has_casting_detail ?castingDet2 .
        ?castingDet2 mus:U2_foresees_use_of_medium_of_performance_of_type <http://data.doremus.org/vocabulary/iaml/mop/sva> ;
                mus:U30_foresees_quantity_of_mop 1 .

        ?casting mus:U23_has_casting_detail ?castingDet3 .
        ?castingDet3 mus:U2_foresees_use_of_medium_of_performance_of_type <http://data.doremus.org/vocabulary/iaml/mop/svc> ;
                mus:U30_foresees_quantity_of_mop 1 .

    }
    GROUP BY ?casting
    HAVING (COUNT(?allCastingDets) = 3)

  }
}